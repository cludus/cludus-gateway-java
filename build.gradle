plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.3'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'com.google.cloud.tools.jib' version '3.3.2'
	id 'maven-publish'
	id "net.researchgate.release" version "3.0.2"
}

group = 'xyz.cludus.gateway'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation "io.micrometer:micrometer-registry-prometheus:1.10.6"
	implementation 'com.google.code.gson:gson:2.10.1'

	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	testCompileOnly 'org.projectlombok:lombok:1.18.30'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
}

tasks.named('test') {
	useJUnitPlatform()
}

def githubActor = System.getenv('GITHUB_ACTOR')
if (githubActor == null) {
	githubActor = 'github'
}
def githubToken = System.getenv('GITHUB_TOKEN')
if (githubToken == null) {
	githubToken = 'github_token'
}

release {
	tagTemplate = '${version}'
}

jib {
	from {
		image = 'amazoncorretto:17'
	}
	to {
		image = 'ghcr.io/cludus/gateway-java'
		tags = ["$version", 'latest']
		auth {
			username = githubActor
			password = githubToken
		}
	}
	container {
		jvmFlags = ['-Xms1024M', '-Xmx3g']
		mainClass = 'xyz.cludus.gateway.CludusGatewayApplication'
		ports = ['8080']
		format = 'OCI'
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/cludus"
			credentials {
				username = githubActor
				password = githubToken
			}
		}
	}
}