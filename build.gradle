buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.2'
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.3'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'com.google.cloud.tools.jib' version '3.3.2'
	id 'maven-publish'
	id "net.researchgate.release" version "3.0.2"
	id 'com.google.protobuf' version "0.9.2"
}

group = 'xyz.cludus'
description = "Cludus's chat gateway server implementation in java."

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

sourceSets {
	src {
		main {
			java {
				srcDirs 'build/generated/source/proto/main/grpc'
				srcDirs 'build/generated/source/proto/main/java'
			}
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation "io.micrometer:micrometer-registry-prometheus:1.10.6"
	implementation 'com.google.code.gson:gson:2.10.1'
	implementation 'com.github.loki4j:loki-logback-appender:1.4.1'

	implementation 'org.springframework.data:spring-data-redis'
	implementation 'redis.clients:jedis:4.3.2'

	implementation 'io.jsonwebtoken:jjwt-api:0.10.8'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.10.8'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.10.8'

	runtimeOnly 'io.grpc:grpc-netty-shaded:1.58.0'
	implementation 'io.grpc:grpc-protobuf:1.58.0'
	implementation 'io.grpc:grpc-stub:1.58.0'
	compileOnly 'org.apache.tomcat:annotations-api:6.0.53' // necessary for Java 9+

	compileOnly 'org.projectlombok:lombok:1.18.30'
	annotationProcessor 'org.projectlombok:lombok:1.18.30'

	testCompileOnly 'org.projectlombok:lombok:1.18.30'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	runtimeOnly 'org.springframework.boot:spring-boot-starter-actuator'
}

protobuf {
	protoc {
		artifact = 'com.google.protobuf:protoc:3.12.2'
	}

	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:1.56.1"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.named('processResources') {
	expand(project.properties)
}

def githubActor = System.getenv().getOrDefault('GITHUB_ACTOR', 'none')
def githubToken = System.getenv().getOrDefault('GITHUB_TOKEN', 'none')

release {
	tagTemplate = '${version}'
}

tasks.named('afterReleaseBuild') {
	dependsOn tasks.jib
}

jib {
	from {
		image = 'amazoncorretto:17'
	}
	to {
		image = 'ghcr.io/cludus/gateway-java'
		tags = ["$version", 'latest']
		auth {
			username = githubActor
			password = githubToken
		}
	}
	container {
		jvmFlags = ['-Xms1024M', '-Xmx3g']
		mainClass = 'xyz.cludus.gateway.CludusGatewayApplication'
		ports = ['8080']
		format = 'OCI'
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
	repositories {
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/cludus"
			credentials {
				username = githubActor
				password = githubToken
			}
		}
	}
}